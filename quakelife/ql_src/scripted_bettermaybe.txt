// Scripts handler (not finished yet.. maybe never lol)
//scripted_sentence in hl_sound.qc

void() scripted_sequence_findtrigger;

void () scripted_sequence_use =
{
//_bprint(ftos(self.spawnflags), " is me spawnflags and ", self.targetname, " is me targetname and ", self.target, " is me target\n");
	local entity targ;
	targ = find(world, targetname, self.m_iszEntity);
	
	if (targ != world) {
		if (self.m_fMoveTo > 0) {
			setorigin(targ, self.origin + '0 0 30');
			targ.npc_originalloc = self.origin + '0 0 30';
			targ.#npc_originalloc = self.origin + '0 0 30';
		}
	}
	
	targ = find(world, classname, self.m_iszEntity);
	
	if (targ != world) {
		if (self.m_fMoveTo > 0) {
			setorigin(targ, self.origin + '0 0 30');
			targ.npc_originalloc = self.origin + '0 0 30';
			targ.#npc_originalloc = self.origin + '0 0 30';
		}
	}
	
	self.use = SUB_Null;
	SUB_UseTargets();
	dremove(self);
};

void () scripted_sequence_touch =
{
	local entity te;
	te = nextent(world);
	while (te != world)
	{
		if ( te.classname == self.m_iszEntity || te.targetname == self.m_iszEntity )
			if ( te.health > 0 ) {
				if (self.think == scripted_sequence_findtrigger)
					self.nextthink = time + .5;
				return;
			}
		te = nextent(te); 
	}
	
	scripted_sequence_use();
};

void () scripted_sequence_findtrigger =
{
	local entity head;
	head = findradius(self.origin, 100);
	while(head)
	{
		if (head.classname == "player") {
			other = head;
			scripted_sequence_touch();
		}
		head = head.chain;
	}
	self.nextthink = time + .5;
};

void () scripted_sequence_fix =
{
//bprint(2,"aint nothing triggers me2\n");
	self.touch = scripted_sequence_touch;
	self.think = scripted_sequence_findtrigger;
	self.nextthink = time + 1;
};

void () FindMyTrigger_think =
{
	local entity te, oself;
	local float foundit, i;
	oself=self;
	
	if (!self.owner) {
		dremove(self);
		return;
	}
	
	self = self.owner;
	
	foundit = 0;
	te = nextent(world);
	while (te != world)
	{
		if ( te.target == self.targetname ) {
			foundit = 1;
			break;
		}
		if ( te.classname == "multi_manager" ) {		// fixme: saner method to look through these :\
			tokenize(te.netname);
			while (argv(i) != string_null && argv(i) != "}") {
				if (argv(i) == self.targetname)
				{
					//bprint(2,"FOUND THIS ONE\n");
					// OK, we found  a multi_manager that trigers us. No need to run automaticall then..
					foundit=1;
					break;
				}
				i++;
			}
			i=0;
		}
		te = nextent(te); 
	}
	
	if (!foundit)
		oself.think1();
	else
		oself.th_die();
	
	self = oself;
	dremove(self);
};

void ( entity for_ent, float wait_time, void() found, void() notfound ) FindMyTrigger =
{
	local entity finder;
	
	finder = spawn();
	finder.owner = for_ent;
	finder.think = FindMyTrigger_think;
	finder.think1 = found;
	finder.th_die = notfound;
	finder.nextthink = time + wait_time;
};

void () scripted_sequence =
{
	if (self.m_iszEntity) {
		self.use = scripted_sequence_use;
		if (!self.targetname || self.spawnflags & 16) {		// if we're not targeted, that generally means we gotta look for something
			self.touch = scripted_sequence_touch;
			self.think = scripted_sequence_findtrigger;
			self.nextthink = time + 1;
		}
		else {
			FindMyTrigger(self, 3, scripted_sequence_fix, SUB_Null);
		}
	}
	else
		trigger_once();
};

// parse centerprint text from titles.txt
void () env_message_use =
{
	local float file, i;
	local string msgstr, a;
	local entity targ;
	
	file = fopen ("titles.txt", 0);
	
	while ( i < 1100 ) {
		a = fgets (file);
		//a = strzone(a);
		
		if (self.has_tesla == 1)
		{
			if (a == "}")
				break;
			if (a != "{") {
				msgstr = strcat(msgstr, a);
				msgstr = strcat(msgstr, "\n");
			}
		}
		
		if (a == self.message) {
			self.has_tesla = 1;
		}
		
		//strunzone(a);
		i++;
	}
	msgstr = strzone(msgstr);
	
	fclose( file );

	targ = find(world, classname, "player");
	while (targ)
	{
		centerprint(targ, msgstr);
		targ = find(targ, classname, "player");
	}
	bprint(2,msgstr);
	
	strunzone(msgstr);
};

void () env_message =
{
	self.use = env_message_use;
};